apply plugin: 'provided-base'

task createWrapper(type: Wrapper) {
    gradleVersion = "2.9"
}


buildscript {

    ext {
        appName = 'ore-infinium'
        gdxVersion = '1.7.1'
        //roboVMVersion = '1.0.0-beta-01'
        aiVersion = '1.4.0'
        artemisVersion = '1.2.1'
        artemisContribVersion = '1.1.0'
        kryonetVersion = '2.22.0-RC1'
        //use + for latest
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }

        jcenter()
    }

    dependencies {
        // lib for artemis-odb weaving.
        // see https://github.com/junkdog/artemis-odb/wiki/Bytecode-weaving
        classpath "net.onedaybeard.artemis:artemis-odb-gradle-plugin:$artemisVersion"

        // introduces support for provided scope gradle keyword.
        //3.0.3 FIXME
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.+'
    }
}

def getDate() {
    def date = new Date()

    def formattedDate = date.format("yyyy-MM-dd-'time'-HH-mm");
    return formattedDate
}

version = '0.1-build-' + getDate()

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"


    repositories {
        mavenLocal()
        mavenCentral()

        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java"

    dependencies {

        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

project(":core") {
    apply plugin: "java"

//    sourceSets.test.java.srcDirs = ["test/"]
    configurations {
        provided
    }

    sourceSets {
         // run the annotation processor compile time.
        main {
            compileClasspath += [configurations.provided]
        }
    }

    dependencies {
        // libgdx core
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"

        //libgdx addons
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-ai:$aiVersion"

        //junit
        //       testCompile "junit:junit:4.11+"

        //kryonet
        compile "com.esotericsoftware:kryonet:$kryonetVersion"

        //artemis-odb
        compile "net.onedaybeard.artemis:artemis-odb:$artemisVersion"
        compile "net.onedaybeard.artemis:artemis-odb-serializer-json-libgdx:$artemisVersion"

        // Artemis-odb annotation processor (see https://github.com/junkdog/artemis-odb/wiki/EntityFactory)
        provided "net.onedaybeard.artemis:artemis-odb-processor:$artemisVersion"

        // Artemis-odb bonus functionality (systems, events, components).
        compile "net.mostlyoriginal.artemis-odb:contrib-core:$artemisContribVersion"
        compile "net.mostlyoriginal.artemis-odb:contrib-jam:$artemisContribVersion"
        compile "net.mostlyoriginal.artemis-odb:contrib-eventbus:$artemisContribVersion"
        compile "net.mostlyoriginal.artemis-odb:contrib-plugin-operations:$artemisContribVersion"
        compile "net.mostlyoriginal.artemis-odb:contrib-plugin-profiler:$artemisContribVersion"
    }
}

project(":matrix") {
    apply plugin: "java"

    dependencies {
        compile project(":core")
        compile "net.onedaybeard.ecs:matrix-cli:0.1.1"
    }

    task generateMatrix(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        main = 'com.artemis.cli.CliApplication'
        args '-c', "$rootProject.projectDir/core/build/classes/main/", '-l', "$rootProject.ext.appName"
    }
    build.finalizedBy generateMatrix
}

tasks.eclipse.doLast {
    delete ".project"
}